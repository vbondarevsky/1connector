#Использовать "../internal"

// BSLLS:ExportVariables-off
Перем ВремяВыполнения Экспорт; // Время выполнения запроса
Перем Cookies Экспорт; // Полученные Cookies
Перем Заголовки Экспорт; // Заголовки ответа
Перем ЭтоПостоянныйРедирект Экспорт; // Ответ содержит постоянный редирект
Перем ЭтоРедирект Экспорт; // Ответ содержит редирект
Перем Кодировка Экспорт; // Кодировка ответа
Перем КодСостояния Экспорт; // Код состояния ответа
Перем URL Экспорт; // URL, по которому был запрошен ответ
// BSLLS:ExportVariables-on

Перем мИсходныйОтвет; // Служебная переменная для хранения объекта HTTPОтвет
Перем мПараметрыПреобразованияJSON; // Служебная переменная для хранения параметров преобразования JSON

Процедура ПриСозданииОбъекта(ИсходныйОтвет, ПараметрыПреобразованияJSON)
	мИсходныйОтвет = ИсходныйОтвет;
	мПараметрыПреобразованияJSON = ПараметрыПреобразованияJSON;
КонецПроцедуры

Функция Json() Экспорт

	Возврат КоннекторHTTPСлужебный.JsonВОбъект(Текст(Кодировка), мПараметрыПреобразованияJSON);

КонецФункции

Функция Текст(КодировкаТекста = Неопределено) Экспорт

	Если Не ЗначениеЗаполнено(КодировкаТекста) Тогда
		Если ЗначениеЗаполнено(Кодировка) Тогда
			КодировкаТекста = Кодировка;
		Иначе
			КодировкаТекста = "utf-8";
		КонецЕсли;
	КонецЕсли;

	Результат = мИсходныйОтвет.ПолучитьТелоКакСтроку(КодировкаТекста);

	Если Результат = Неопределено Тогда
		Результат = "";
	КонецЕсли;

	Возврат Результат;

КонецФункции

Функция ДвоичныеДанные() Экспорт

	Возврат мИсходныйОтвет.ПолучитьТелоКакДвоичныеДанные();

КонецФункции